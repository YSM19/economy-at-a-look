plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.5'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.liquibase.gradle' version '2.2.0'
}

group = 'com.at_a_look'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	
	// Swagger UI
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'
	
	// Jackson for JSON
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	
	// JWT
	implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'
	
	// Database - MySQL
	runtimeOnly 'com.mysql:mysql-connector-j'
	
	// Hibernate Commons Annotations (Hibernate 6.x compatibility)
	implementation 'org.hibernate.common:hibernate-commons-annotations:6.0.6.Final'
	
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	implementation 'org.liquibase:liquibase-core:4.29.1'
	liquibaseRuntime 'org.liquibase:liquibase-core:4.29.1'
	liquibaseRuntime 'com.mysql:mysql-connector-j'
	liquibaseRuntime 'org.springframework.boot:spring-boot-starter-jdbc'
}

// Liquibase 설정
liquibase {
	activities {
		main {
			// 기본 설정 (dev 환경)
			changeLogFile 'src/main/resources/db/changelog/db.changelog-master.xml'
			url 'jdbc:mysql://localhost:3306/economy?useSSL=false&useUnicode=true&serverTimezone=Asia/Seoul'
			username 'root'
			password '1234'
			driver 'com.mysql.cj.jdbc.Driver'
			defaultSchemaName 'economy'
		}
		prod {
			// 운영 환경 설정
			changeLogFile 'src/main/resources/db/changelog/db.changelog-master.xml'
			url System.getenv('DB_URL') ?: 'jdbc:mysql://localhost:3306/economy'
			username System.getenv('DB_USERNAME') ?: 'root'
			password System.getenv('DB_PASSWORD') ?: '1234'
			driver 'com.mysql.cj.jdbc.Driver'
			defaultSchemaName 'economy'
		}
	}
	runList = 'main'
}

// 프로필에 따른 빌드 설정
def activeProfile = findProperty('profile') ?: 'dev'

bootJar {
    manifest {
        attributes(
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Spring-Boot-Profile': activeProfile
        )
    }
}

tasks.named('test') {
	useJUnitPlatform()
}



